plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'io.bakir.lab.derivatives'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        groovy.srcDir file('src/integrationTest/groovy')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
    e2eTest {
        groovy.srcDir file('src/e2eTest/groovy')
        resources.srcDir file('src/e2eTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    e2eTestImplementation.extendsFrom testImplementation
    e2eTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    // Spring implementation dependencies
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Spring Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'

    // Junit Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
    integrationTestImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'

    e2eTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    e2eTestImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
    e2eTestImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
}

tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter tasks.test
    useJUnitPlatform()
}

task e2eTest(type: Test) {
    description = 'Runs end-to-end tests.'
    group = 'verification'
    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
    shouldRunAfter integrationTest
    useJUnitPlatform()
}

check.dependsOn integrationTest, e2eTest